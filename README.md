# Global-Solution-Disruptive-Architectures-Iot-Iob-Generative-Ia

## üíß Projeto FloodGuardian: Sistema Inteligente de Preven√ß√£o e Resposta a Enchentes
### Objetivo
Monitorar √°reas de risco em tempo real usando IoT e sat√©lites (como a Carta Internacional), prever 
enchentes e alertar moradores via app mobile, al√©m de permitir atua√ß√£o da Defesa Civil com apoio de 
pain√©is e APIs. 

## Descri√ß√£o do Projeto
Este projeto monitora o n√≠vel e o volume de um reservat√≥rio usando um sensor ultrass√¥nico HC-SR04, ESP32, display LCD I2C, LEDs indicadores e um m√≥dulo RTC DS3231 para registro da data e hora. Os dados s√£o enviados para duas plataformas diferentes:
- üì° ThingSpeak para visualiza√ß√£o remota em nuvem
- ‚òÅÔ∏è MQTT + Node-RED para monitoramento via dashboard local

## ‚öôÔ∏è Funcionalidades
- Leitura da altura da √°gua com sensor ultrass√¥nico HC-SR04
- C√°lculo do volume (baseado no formato cil√≠ndrico do reservat√≥rio)
- Exibi√ß√£o de dados em LCD 16x2
- Indica√ß√£o de n√≠veis com LEDs (Verde, Amarelo, Vermelho)
- Envio de dados para ThingSpeak (via HTTP) ou para Node-RED (via MQTT)
- Registro do hor√°rio de cada medi√ß√£o com o RTC DS3231

## Simula√ß√£o no Wokwi
O Wokwi permite simular projetos com ESP32 de forma realista. Ambos os c√≥digos podem ser testados diretamente na plataforma.
### Hardware
| Componente                      | Fun√ß√£o Principal                 |
| ------------------------------- | -------------------------------- |
| ESP32                           | Controlador principal            |
| HC-SR04                         | Leitura da dist√¢ncia da √°gua     |
| LCD 16x2 com I2C                | Exibi√ß√£o do n√≠vel e volume       |
| LEDs (verde, amarelo, vermelho) | Indica√ß√£o visual do n√≠vel        |
| RTC DS3231                      | Registro do hor√°rio das medi√ß√µes |

![image](https://github.com/user-attachments/assets/3dbe4626-52d9-4bcb-b4a3-86c8c1ed90f7)

## üì° Wokwi com ThingSpeak (via HTTP)
O ThingSpeak √© uma plataforma na nuvem para armazenamento, visualiza√ß√£o e an√°lise de dados IoT. Ele permite que voc√™ crie "canais" para receber dados de sensores em tempo real, sem necessidade de um servidor pr√≥prio.
### Como o projeto funciona com o ThingSpeak no Wokwi:
#### Simula√ß√£o no Wokwi:
- O ESP32 √© simulado com sensores como HC-SR04 (ultrass√¥nico), RTC DS3231 e display LCD.
- O c√≥digo coleta o n√≠vel da √°gua, calcula o volume e obt√©m o hor√°rio atual.
#### Conex√£o com Wi-Fi:
- O ESP32 (virtual) se conecta √† rede simulada do Wokwi (Wokwi-GUEST), que n√£o exige senha.
- Essa rede permite conex√µes reais de internet, inclusive para servi√ßos externos como ThingSpeak.
#### Envio de dados via HTTP GET:
- O c√≥digo monta uma URL com os valores coletados (n√≠vel, volume, hor√°rio etc.).
- Usa a biblioteca HTTPClient para enviar uma requisi√ß√£o HTTP GET para o endpoint da ThingSpeak.
  
![image](https://github.com/user-attachments/assets/3a95386c-4dde-4851-ae51-c6e4faa60018)

## ‚òÅÔ∏è Wokwi com Node-RED via MQTT
O MQTT (Message Queuing Telemetry Transport) √© um protocolo leve de mensagens usado em IoT. Ele √© baseado no modelo publicador/assinante:
- O dispositivo (ESP32) publica mensagens em um t√≥pico.
- Node-RED (ou outro cliente) assina esse t√≥pico e recebe os dados.
### Como funciona a comunica√ß√£o Wokwi ‚Üí Node-RED (via HiveMQ):
#### ESP32 simulado no Wokwi:
- Coleta os dados como na vers√£o anterior.
- Em vez de enviar via HTTP, ele publica via MQTT no t√≥pico:
> globalsolution/medidor/nivel
#### Broker MQTT (HiveMQ):
- Utiliza o broker p√∫blico da HiveMQ (broker.hivemq.com) na porta 1883.
- Serve como ponto de encontro entre o ESP32 e o Node-RED.
#### Node-RED:
- Conecta-se ao mesmo broker MQTT.
- Usa um n√≥ mqtt in para assinar o t√≥pico globalsolution/medidor/nivel.
- Recebe mensagens no formato JSON, como:
  
```json
{
  "nivel_cm": 150,
  "volume_litros": 11780,
  "distancia_cm": 150,
  "horario": "13:15:20"
}
```
#### Dashboard no Node-RED:
- Um n√≥ function separa o JSON em 3 fluxos.
- Os dados s√£o exibidos em widgets:
  - Gauge (volume)
  - Textos (n√≠vel e hor√°rio)

### Fluxo Node-RED
![image](https://github.com/user-attachments/assets/78af34a9-e58f-4dfd-ad16-33cce06ad230)
### Dashboard
![image](https://github.com/user-attachments/assets/743804f1-669d-4db2-ac3a-459c539ad1dc)

## C√≥digo-fonte
### üì° Wokwi com ThingSpeak
---
### ‚öôÔ∏è Componentes Utilizados

- ESP32
- Sensor Ultrass√¥nico (HC-SR04)
- Display LCD 16x2 com interface I2C
- RTC (Real Time Clock) DS3231
- LEDs (verde, amarelo, vermelho)
- Conex√£o Wi-Fi
- Plataforma ThingSpeak
---
### ‚öôÔ∏è Bibliotecas Utilizadas

- `Wire.h` - Comunica√ß√£o I2C
- `LiquidCrystal_I2C.h` - Controle do display LCD
- `RTClib.h` - Controle do RTC
- `WiFi.h` - Conex√£o com a rede Wi-Fi
- `HTTPClient.h` - Envio de dados via HTTP
---
### ‚öôÔ∏è Funcionamento

1. O **sensor ultrass√¥nico** mede a dist√¢ncia da √°gua at√© o topo do reservat√≥rio.
2. A altura da √°gua √© usada para calcular o **volume** em litros.
3. Os dados s√£o:
   - Exibidos em tempo real no **display LCD**;
   - Indicados por **LEDs**:
     - Verde: at√© 200 cm
     - Amarelo: at√© 300 cm
     - Vermelho: acima de 300 cm
   - Enviados para o **ThingSpeak** com hor√°rio atual via RTC.
---
### ‚öôÔ∏è ThingSpeak
O projeto envia os seguintes dados para o ThingSpeak:
- `field1`: N√≠vel da √°gua (cm)
- `field2`: Volume calculado (litros)
- `field3`: Hor√°rio da medi√ß√£o
- `field4`: Repeti√ß√£o do n√≠vel (pode ser usado como gauge)
**API Key:** `N18H1NFNM1SSJCND`
---
### ‚öôÔ∏è F√≥rmulas utilizadas
##### Volume do reservat√≥rio (cilindro):
```cpp
volume_cm3 = œÄ * raio¬≤ * altura
volume_litros = volume_cm3 / 1000
```
## C√≥digo-fonte
### üì° Wokwi com ThingSpeak (via HTTP)
```cpp
// Bibliotecas para comunica√ß√£o I2C, RTC, Wi-Fi e requisi√ß√µes HTTP
#include <Wire.h>                    // Comunica√ß√£o I2C (RTC e LCD)
#include <LiquidCrystal_I2C.h>      // Controle de display LCD 16x2 via I2C
#include <RTClib.h>                 // Rel√≥gio de tempo real DS3231
#include <WiFi.h>                   // Wi-Fi para ESP32
#include <HTTPClient.h>             // Cliente HTTP para envio via ThingSpeak

// Pinos do sensor ultrass√¥nico
#define TRIG_PIN 5
#define ECHO_PIN 18

// LEDs para indicar o n√≠vel da √°gua
#define LED_VERDE 13
#define LED_AMARELO 12
#define LED_VERMELHO 14

// Configura√ß√µes da rede Wi-Fi e API do ThingSpeak
const char* ssid = "Wokwi-GUEST";          // Rede Wi-Fi simulada no Wokwi
const char* password = "";                 // Sem senha no Wokwi-GUEST
const char* server = "http://api.thingspeak.com/update";  // Endpoint de envio
String apiKey = "N18H1NFNM1SSJCND";        // Chave da API do canal ThingSpeak

// LCD no endere√ßo I2C 0x27 com 16 colunas e 2 linhas
LiquidCrystal_I2C lcd(0x27, 16, 2);

// RTC DS3231 para registrar hor√°rio da medi√ß√£o
RTC_DS3231 rtc;

// Par√¢metros do reservat√≥rio
const float altura_total_cm = 400.0; // Altura total do tanque
const float raio_cm = 50.0;          // Raio da base do tanque

float distanciaAnterior = -1;  // √öltima dist√¢ncia medida (evita envio repetido)

// Fun√ß√£o de inicializa√ß√£o
void setup() {
  Serial.begin(115200);  // Inicializa a comunica√ß√£o serial

  // Configura os pinos dos sensores e LEDs
  pinMode(TRIG_PIN, OUTPUT);
  pinMode(ECHO_PIN, INPUT);
  pinMode(LED_VERDE, OUTPUT);
  pinMode(LED_AMARELO, OUTPUT);
  pinMode(LED_VERMELHO, OUTPUT);

  // Inicializa comunica√ß√£o I2C e o LCD
  Wire.begin(21, 22);      // Pinos padr√£o I2C do ESP32
  lcd.init();
  lcd.backlight();
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Nivel:       cm");
  lcd.setCursor(0, 1);
  lcd.print("Volume:      L");

  // Inicializa o RTC
  if (!rtc.begin()) while (1); // Para tudo se RTC falhar
  if (rtc.lostPower()) rtc.adjust(DateTime(F(__DATE__), F(__TIME__))); // Ajusta hor√°rio

  // Conecta ao Wi-Fi
  WiFi.begin(ssid, password);
  Serial.print("Conectando ao Wi-Fi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\nWi-Fi conectado!");
}

// Fun√ß√£o para medir a dist√¢ncia da √°gua usando o sensor ultrass√¥nico
float medirDistancia() {
  digitalWrite(TRIG_PIN, LOW);
  delayMicroseconds(2);
  digitalWrite(TRIG_PIN, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG_PIN, LOW);
  long duracao = pulseIn(ECHO_PIN, HIGH, 60000);  // Tempo do eco (timeout: 60ms)
  if (duracao == 0) return -1;                    // Retorna erro se n√£o houver retorno
  return duracao * 0.0343 / 2;                    // Converte para cent√≠metros
}

// Fun√ß√£o para acender LEDs conforme n√≠vel da √°gua
void atualizarLEDs(float nivel) {
  digitalWrite(LED_VERDE, LOW);
  digitalWrite(LED_AMARELO, LOW);
  digitalWrite(LED_VERMELHO, LOW);

  if (nivel <= 200) digitalWrite(LED_VERDE, HIGH);         // N√≠vel bom
  else if (nivel <= 300) digitalWrite(LED_AMARELO, HIGH);  // Alerta
  else digitalWrite(LED_VERMELHO, HIGH);                   // Cr√≠tico
}

// Fun√ß√£o para exibir dados no LCD
void atualizarLCD(float nivel, float volume) {
  lcd.setCursor(7, 0);
  lcd.print("     ");       // Limpa antes de imprimir
  lcd.setCursor(7, 0);
  lcd.print(nivel, 0);      // N√≠vel da √°gua sem casas decimais

  lcd.setCursor(8, 1);
  lcd.print("     ");
  lcd.setCursor(8, 1);
  lcd.print(volume, 0);     // Volume da √°gua sem casas decimais
}

// Fun√ß√£o para enviar dados ao ThingSpeak via HTTP
void enviarParaThingSpeak(float nivel, float volume, String horario, float gauge) {
  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;
    
    // Monta URL com par√¢metros
    String url = server;
    url += "?api_key=" + apiKey;
    url += "&field1=" + String(nivel, 2);     // campo 1: n√≠vel da √°gua
    url += "&field2=" + String(volume, 2);    // campo 2: volume
    url += "&field3=" + horario;              // campo 3: hor√°rio
    url += "&field4=" + String(gauge);        // campo 4: redund√¢ncia da medi√ß√£o

    http.begin(url);                // Inicia a requisi√ß√£o
    int httpCode = http.GET();      // Envia GET para o ThingSpeak

    if (httpCode > 0) {
      Serial.println("Enviado para ThingSpeak!");
    } else {
      Serial.print("Erro ao enviar: ");
      Serial.println(http.errorToString(httpCode).c_str());
    }

    http.end(); // Finaliza a conex√£o
  } else {
    Serial.println("WiFi desconectado");
  }
}

// Loop principal do c√≥digo
void loop() {
  float distancia = medirDistancia();

  // Verifica se a leitura √© v√°lida
  if (distancia < 0 || distancia > altura_total_cm) {
    lcd.setCursor(0, 0);
    lcd.print("Erro leitura     ");
    Serial.println("Erro de leitura");
    delay(1000);
    return;
  }

  // Calcula o n√≠vel da √°gua e volume do cilindro
  float nivel_agua = distancia;
  float volume_cm3 = 3.1416 * pow(raio_cm, 2) * nivel_agua;
  float volume_litros = volume_cm3 / 1000.0;

  atualizarLEDs(nivel_agua);  // Mostra estado via LEDs

  // Verifica se houve varia√ß√£o significativa antes de enviar
  if (abs(distancia - distanciaAnterior) > 0.5) {
    atualizarLCD(nivel_agua, volume_litros);

    // Captura hor√°rio atual do RTC
    DateTime now = rtc.now();
    String horarioFormatado = String(now.hour()) + ":" +
                              String(now.minute()) + ":" +
                              String(now.second());

    // Exibe informa√ß√µes no monitor serial
    Serial.print("[");
    Serial.print(horarioFormatado);
    Serial.print("] Nivel: ");
    Serial.print(nivel_agua, 2);
    Serial.print(" cm | Volume: ");
    Serial.print(volume_litros, 2);
    Serial.println(" L");

    // Envia para ThingSpeak
    enviarParaThingSpeak(nivel_agua, volume_litros, horarioFormatado, nivel_agua);

    // Atualiza a √∫ltima leitura v√°lida
    distanciaAnterior = distancia;
  }

  delay(5000); // Aguarda 5 segundos entre leituras
}

```

## C√≥digo-fonte
### ‚òÅÔ∏è Wokwi com Node-RED via MQTT
---
### ‚öôÔ∏è Componentes Utilizados
- ESP32
- Sensor Ultrass√¥nico (HC-SR04)
- RTC (DS3231)
- Display LCD 16x2 com I2C
- LEDs (verde, amarelo, vermelho)
- Broker MQTT p√∫blico: [HiveMQ](http://broker.hivemq.com)
- Wi-Fi
  
### ‚öôÔ∏è Bibliotecas Utilizadas
- `Wire.h` ‚Äì Comunica√ß√£o I2C
- `LiquidCrystal_I2C.h` ‚Äì Controle do LCD
- `RTClib.h` ‚Äì Rel√≥gio de tempo real (RTC)
- `WiFi.h` ‚Äì Conex√£o com rede Wi-Fi
- `PubSubClient.h` ‚Äì Comunica√ß√£o com servidor MQTT
- `ArduinoJson.h` ‚Äì Cria√ß√£o e envio de objetos JSON
---
### ‚öôÔ∏è Fluxo de Funcionamento

1. O **sensor ultrass√¥nico** mede a dist√¢ncia da √°gua at√© o topo.
2. A dist√¢ncia √© convertida em:
   - **N√≠vel da √°gua** (em cm)
   - **Volume** (em litros)
3. Os dados s√£o:
   - Exibidos no **LCD**
   - Indicados por **LEDs**
   - Enviados via **MQTT** no formato JSON
---
### ‚öôÔ∏è T√≥pico MQTT
- **Broker:** `broker.hivemq.com`
- **T√≥pico:** `globalsolution/medidor/nivel`
- **Porta:** 1883 (padr√£o MQTT)
---
### ‚öôÔ∏è Formato da mensagem JSON enviada:
```json
{
  "nivel_cm": 182.34,
  "volume_litros": 14344.67,
  "distancia_cm": 182.34,
  "horario": "14:26:05"
}
```
### ‚òÅÔ∏è Wokwi com Node-RED via MQTT :
```cpp
// Bibliotecas necess√°rias para comunica√ß√£o, sensores e MQTT
#include <Wire.h>                    // Comunica√ß√£o I2C
#include <LiquidCrystal_I2C.h>      // Controle do display LCD 16x2 via I2C
#include <RTClib.h>                 // RTC DS3231 para data e hora
#include <WiFi.h>                   // Conex√£o Wi-Fi (ESP32)
#include <PubSubClient.h>          // Comunica√ß√£o MQTT
#include <ArduinoJson.h>           // Para formatar os dados em JSON

// Pinos do sensor ultrass√¥nico
#define TRIG_PIN 5
#define ECHO_PIN 18

// Pinos dos LEDs indicadores de n√≠vel
#define LED_VERDE 13
#define LED_AMARELO 12
#define LED_VERMELHO 14

// Dados da rede Wi-Fi simulada no Wokwi
const char* ssid = "Wokwi-GUEST";
const char* password = "";

// Broker MQTT (HiveMQ p√∫blico) e t√≥pico onde os dados ser√£o publicados
const char* mqtt_server = "broker.hivemq.com";
const char* mqtt_topic = "globalsolution/medidor/nivel";

// Cria√ß√£o dos objetos de rede
WiFiClient espClient;
PubSubClient client(espClient);

// LCD I2C no endere√ßo 0x27, 16 colunas x 2 linhas
LiquidCrystal_I2C lcd(0x27, 16, 2);

// Objeto para acessar o RTC
RTC_DS3231 rtc;

// Medidas do reservat√≥rio
const float altura_total_cm = 450.0;  // Altura m√°xima do reservat√≥rio
const float raio_cm = 50.0;           // Raio do reservat√≥rio

float distanciaAnterior = -1;  // Usado para evitar envio repetido de dados semelhantes

// Fun√ß√£o para conectar ao Wi-Fi
void setup_wifi() {
  WiFi.begin(ssid, password);
  Serial.print("Conectando WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("Conectado!");
}

// Fun√ß√£o para reconectar ao MQTT caso a conex√£o caia
void reconnect() {
  while (!client.connected()) {
    Serial.print("Conectando MQTT...");
    if (client.connect("ESP32Client1234")) {  // Nome do cliente MQTT
      Serial.println("Conectado!");
    } else {
      Serial.print("Falha, rc=");
      Serial.print(client.state());
      Serial.println(" Tentando de novo em 2s");
      delay(2000);
    }
  }
}

// Mede a dist√¢ncia da √°gua usando o sensor ultrass√¥nico
float medirDistancia() {
  digitalWrite(TRIG_PIN, LOW);
  delayMicroseconds(2);
  digitalWrite(TRIG_PIN, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG_PIN, LOW);

  long duracao = pulseIn(ECHO_PIN, HIGH, 60000);  // Tempo do eco (timeout 60ms)
  if (duracao == 0) return -1;                    // Retorna -1 em caso de falha
  return duracao * 0.0343 / 2;                    // Converte para cent√≠metros
}

// Atualiza os LEDs conforme o n√≠vel da √°gua
void atualizarLEDs(float nivel) {
  digitalWrite(LED_VERDE, LOW);
  digitalWrite(LED_AMARELO, LOW);
  digitalWrite(LED_VERMELHO, LOW);

  if (nivel <= 200) digitalWrite(LED_VERDE, HIGH);        // N√≠vel bom
  else if (nivel <= 300) digitalWrite(LED_AMARELO, HIGH); // Alerta
  else digitalWrite(LED_VERMELHO, HIGH);                  // Cr√≠tico
}

// Atualiza o display LCD com n√≠vel e volume
void atualizarLCD(float nivel, float volume) {
  lcd.setCursor(7, 0);    // Posi√ß√£o para mostrar n√≠vel
  lcd.print("     ");     // Limpa √°rea
  lcd.setCursor(7, 0);
  lcd.print(nivel, 0);    // Mostra n√≠vel (sem decimais)

  lcd.setCursor(8, 1);    // Posi√ß√£o para mostrar volume
  lcd.print("     ");
  lcd.setCursor(8, 1);
  lcd.print(volume, 0);   // Mostra volume (sem decimais)
}

// Envia os dados via MQTT no formato JSON
void enviarMQTT(float nivel, float volume, String horario, float distancia) {
  StaticJsonDocument<256> doc;
  doc["nivel_cm"] = nivel;
  doc["volume_litros"] = volume;
  doc["distancia_cm"] = distancia;
  doc["horario"] = horario;

  char buffer[256];
  serializeJson(doc, buffer);  // Converte para string JSON

  if (!client.publish(mqtt_topic, buffer)) {
    Serial.println("Erro ao publicar MQTT");
  } else {
    Serial.println("MQTT publicado:");
    Serial.println(buffer);  // Mostra o JSON enviado
  }
}

// Configura√ß√µes iniciais
void setup() {
  Serial.begin(115200);  // Inicia porta serial para debug

  // Configura os pinos
  pinMode(TRIG_PIN, OUTPUT);
  pinMode(ECHO_PIN, INPUT);
  pinMode(LED_VERDE, OUTPUT);
  pinMode(LED_AMARELO, OUTPUT);
  pinMode(LED_VERMELHO, OUTPUT);

  // Inicializa o barramento I2C com pinos padr√£o do ESP32 (GPIO21 = SDA, GPIO22 = SCL)
  Wire.begin(21, 22);

  // Inicia o LCD
  lcd.init();
  lcd.backlight();
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Nivel:       cm");
  lcd.setCursor(0, 1);
  lcd.print("Volume:      L");

  // Inicia o RTC
  if (!rtc.begin()) {
    Serial.println("Erro no RTC");
    while (1);  // Para o programa se falhar
  }

  // Ajusta o RTC se estiver sem energia
  if (rtc.lostPower()) {
    rtc.adjust(DateTime(F(__DATE__), F(__TIME__)));
  }

  setup_wifi();                 // Conecta ao Wi-Fi
  client.setServer(mqtt_server, 1883);  // Configura o broker MQTT
}

// Loop principal do programa
void loop() {
  if (!client.connected()) {
    reconnect();  // Reconecta MQTT se cair
  }
  client.loop();  // Mant√©m a comunica√ß√£o ativa

  float distancia = medirDistancia();

  // Verifica se leitura da dist√¢ncia √© v√°lida
  if (distancia < 0 || distancia > altura_total_cm) {
    lcd.setCursor(0, 0);
    lcd.print("Erro leitura     ");
    Serial.println("Erro de leitura");
    delay(1000);
    return;
  }

  // Calcula o n√≠vel da √°gua e o volume do reservat√≥rio
  float nivel_agua = distancia;
  float volume_cm3 = 3.1416 * pow(raio_cm, 2) * nivel_agua;
  float volume_litros = volume_cm3 / 1000.0;

  atualizarLEDs(nivel_agua);  // Atualiza LEDs conforme o n√≠vel

  // Evita envio se n√£o houver altera√ß√£o significativa
  if (abs(distancia - distanciaAnterior) > 0.5) {
    atualizarLCD(nivel_agua, volume_litros);  // Atualiza display LCD

    DateTime now = rtc.now();
    String horario = String(now.hour()) + ":" +
                     String(now.minute()) + ":" +
                     String(now.second());

    // Exibe no monitor serial
    Serial.printf("[%s] Nivel: %.2f cm | Volume: %.2f L\n", horario.c_str(), nivel_agua, volume_litros);

    // Envia dados via MQTT
    enviarMQTT(nivel_agua, volume_litros, horario, distancia);

    // Armazena a √∫ltima dist√¢ncia registrada
    distanciaAnterior = distancia;
  }

  delay(3000);  // Aguarda 3 segundos antes da pr√≥xima medi√ß√£o
}
```
## Integrantes
- Eduardo Eiki de Sousa Lopes Yamada RM 554921
- Nicollas Frei Eleoterio RM 557647
- Heitor Pereira Duarte RM 558208







